#compdef pyb

local -a simple_opts extended_opts
simple_opts=(
        '--start-project[Initialize a new project structure]'
        '-v[Verbose output]'
        '-X[Debug output]'
        '-h[Show help]'
)
extended_opts=(
        '-q[Quiet mode]'
        '-Q[Very quiet mode]'
        '-C[Disable colored output]'
        '--update-project[Update build descriptors and python project structure]'
        '-t[Show available tasks]'
        '-T[List execution plan tasks]'
)

__pyb_tasks(){
    local tasks;
    pyb -Qt | while read TASK_LINE; do
        tasks+=( $TASK_LINE )
    done
    _describe -t tasks 'tasks' tasks
}

# no pyb available at all
if ! command -v pyb > /dev/null 2>&1; then
    _message "unable to complete, no pyb found :("

# no build.py in current directory, presumably
elif ! pyb -Qt > /dev/null 2>&1; then
    _message "unable to find a build.py, limited completion available"
    _arguments -s $simple_opts

# the full monty
else
    _arguments -C '*:Task:->tasks' $simple_opts $extended_opts

    case "$state" in
        (tasks)
            _arguments -s $simple_opts $extended_opts
            __pyb_tasks
        ;;
    esac
fi
